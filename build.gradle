// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versions = [
            "kotlin": "1.4.32",
            "buildToolsVersion": "30.0.3",
            "compileSdkVersion":31,
            "minSdkVersion": 23,
            "targetSdkVersion": 31,
            "versionCode": 1,
            "versionName": "1.0"
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.43'
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {

            android {
                buildToolsVersion versions.buildToolsVersion
                compileSdkVersion versions.compileSdkVersion

                defaultConfig {
                    minSdkVersion versions.minSdkVersion
                    targetSdkVersion versions.targetSdkVersion
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }


                variantFilter { variant ->
                    def name_types = variant.flavors*.name
                    def name = name_types.join()
                    if (variant.buildType.name == "release" && (name.contains("development") || name.contains("stagging"))) {
                        setIgnore(true)
                    }

                }
                buildTypes {
                    release {
                        minifyEnabled true
                        useProguard true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                    qcTesting {
                        initWith release
                    }
                }
                def BASE_URL = "BASE_URL"
                flavorDimensions "mode"
                productFlavors {
                    live {
                        dimension "mode"
                        buildConfigField "String", BASE_URL, '"https://api.themoviedb.org/3"'
                    }
                    development {
                        dimension "mode"
                        buildConfigField "String", BASE_URL, '"https://api.themoviedb.org/3"'

                    }
                    stagging {
                        dimension "mode"
                        buildConfigField "String", BASE_URL, '"https://api.themoviedb.org/3"'

                    }
                }

                buildFeatures {
                    viewBinding = true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}